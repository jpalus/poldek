dnl Process this file with autoconf to produce a configure script.
dnl $Id$

AC_INIT(capreq.c)

VERSION=0.15.4
PACKAGE=poldek

AM_INIT_AUTOMAKE($PACKAGE, $VERSION)
AM_CONFIG_HEADER(config.h)

CFLAGS="$CFLAGS -g -Wall -W"
LDFLAGS="$LDFLAGS"
AC_DEFINE(ENABLE_VFILE_RPMIO)

dnl Checks for programs.
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_RANLIB
AC_CHECK_PROG(POD2MAN, pod2man, pod2man)

dnl Process features.

AC_ARG_ENABLE(imode,
[  --disable-imode               turn off interactive mode],
INTERACTIVE_MODE=$enableval, INTERACTIVE_MODE=yes)

if test "${INTERACTIVE_MODE}." != "yes."; then
	AM_CONDITIONAL(ENABLE_INTERACTIVE_MODE, false)	
else 
	AC_CHECK_LIB(readline, readline,,AC_MSG_ERROR("readline not found"))
	AC_CHECK_LIB(pcre, pcre_compile,,AC_MSG_ERROR("pcre not found"))
	AM_CONDITIONAL(ENABLE_INTERACTIVE_MODE, true)
	AC_DEFINE(ENABLE_INTERACTIVE_MODE)
	
	AC_CHECK_FUNCS(rl_completion_matches, have_rl_4_2=yes, have_rl_4_2=no, 
		     $CONF_IN_LDFLAGS)
	if test "${have_rl_4_2}." = "yes."; then
		AC_DEFINE(HAVE_READLINE_4_2)
	fi
fi


AC_ARG_WITH(curl,
[  --without-curl                compile without libcurl],
ENABLE_VFILE_CURL=$enableval, ENABLE_VFILE_CURL=yes)
if test "${ENABLE_VFILE_CURL}." != "yes."; then
	AM_CONDITIONAL(ENABLE_VFILE_CURL, false)
else 
	AC_CHECK_LIB(curl, curl_easy_init,,AC_MSG_ERROR("libcurl not found"))
	AM_CONDITIONAL(ENABLE_VFILE_CURL, true)
	AC_DEFINE(ENABLE_VFILE_CURL)
fi


AC_ARG_ENABLE(static,
[  --enable-static      build static binary],
ENABLE_STATIC=$enableval, ENABLE_STATIC=no)

if test "${ENABLE_STATIC}." = "yes."; then
	poldek_LDFLAGS="-static"
	LIBS="$LIBS -ldb1"

	if test "${INTERACTIVE_MODE}." = "yes."; then
		LIBS="$LIBS -lncurses -ltinfo"
	fi

	if test "${ENABLE_VFILE_CURL}." = "yes."; then 
		LIBS="$LIBS $(curl-config --libs)"
	fi
fi

AC_SUBST(poldek_LDFLAGS)


AC_ARG_ENABLE(trace,
[  --enable-trace    turn on trace messages],
ENABLE_TRACE=$enableval, ENABLE_TRACE=no)

if test "${ENABLE_TRACE}." = "yes."; then
	AC_DEFINE(ENABLE_TRACE)
fi


dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_TYPE_MODE_T
AC_TYPE_OFF_T
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_HEADER_TIME


AC_CHECK_MEMBER(struct tm.tm_gmtoff,
	AC_DEFINE(HAVE_TM_GMTOFF),
	AC_CHECK_MEMBER(struct tm.__tm_gmtoff, AC_DEFINE(HAVE_TM___GMTOFF)
	,,[#include <time.h>]),
[#include <time.h>])


dnl Checks for library functions.
AC_FUNC_ALLOCA
AC_FUNC_FNMATCH
AC_FUNC_STRFTIME
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(gettimeofday mkdir strdup uname)
AC_CHECK_FUNCS(strerror strstr strtol strtoul strsignal)
AC_CHECK_FUNCS(mallopt)

dnl Checks for header files.
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS(fcntl.h limits.h malloc.h)
AC_CHECK_HEADERS(sys/file.h sys/time.h syslog.h unistd.h)

AC_CHECK_HEADER(stdint.h,,AC_MSG_ERROR("C9X compiler is needed by $PACKAGE"))
AC_CHECK_HEADER(obstack.h,,AC_MSG_ERROR("obstacks are needed by $PACKAGE"))
AC_CHECK_HEADER(argp.h,,AC_MSG_ERROR("missing required argp.h"))
AC_CHECK_HEADER(rpm/rpmlib.h,,AC_MSG_ERROR("missing required rpmlib.h"))

AC_MSG_CHECKING(for rpm 4.x)
#RPMDBI_PACKAGES 
AC_EGREP_CPP(yes,
     [#include <rpm/rpmlib.h>
      #ifdef RPMDBI_PACKAGES
       yes
      #endif
     ], is_rpm4=yes, is_rpm4=no)
if test ${is_rpm4}. = yes. ; then
	AC_MSG_RESULT(yes)
	AC_DEFINE(HAVE_RPM_4_0)
	AC_EGREP_CPP(RPMSENSE_SCRIPT_PRE, 
		[ #include <rpm/rpmlib.h>
		], is_rpm_extdeps=yes, is_rpm_extdeps=no)
	if test ${is_rpm_extdeps}. = yes. ; then 
		AC_DEFINE(HAVE_RPM_EXTDEPS)
	fi
else 
	AC_MSG_RESULT(no)
fi


dnl use local ../trurlib copy if it exists
CONF_IN_LDFLAGS=
if test -f "../trurlib/trurl/trurl.h"; then
	AC_MSG_CHECKING(for local trurlib copy in ../trurlib)
	AC_MSG_RESULT([yes])
	TRURL_INCLUDE="-I\$(top_srcdir)/../trurlib"
	TRURL_LDFLAG="-L\$(top_srcdir)/../trurlib"
	AC_SUBST(TRURL_INCLUDE)
	AC_SUBST(TRURL_LDFLAG)
	LDFLAGS="$LDFLAGS -L../trurlib"
else 
	AC_CHECK_HEADER(trurl/trurl.h,,AC_MSG_ERROR("trurl.h is missing"))
fi

dnl Checks for libraries.
AC_CHECK_LIB(z, gzopen)
AC_CHECK_LIB(bz2, BZ2_bzCompress)
AC_CHECK_LIB(db, db_create)
AC_CHECK_LIB(popt, poptReadConfigFile)

if test ${is_rpm4}. = yes. ; then
	AC_CHECK_LIB(rpmio, rpmCleanPath,,AC_MSG_ERROR("rpmio not found"),
		    -lpopt -ldb -lz -lbz2 -lrpm)	
fi

AC_CHECK_LIB(rpm, rpmReadPackageInfo,,AC_MSG_ERROR("rpmlib not found"),
	    -lpopt -ldb -lz -lbz2)

AC_CHECK_FUNCS(rpmMachineScore,, AC_MSG_ERROR("rpmlib not found"),
	    -lpopt -ldb -lz -lbz2)

dnl trullib 0.43.1 has n_buf_it_get() and n_buf_ptr()
AC_CHECK_LIB(trurl, n_buf_it_get,,AC_MSG_ERROR("trurlib >= 0.43.1 not found"), 
	     $CONF_IN_LDFLAGS)
AC_CHECK_FUNCS(n_buf_ptr,,AC_MSG_ERROR("trurlib >= 0.43.1 not found"), 
	     $CONF_IN_LDFLAGS)

AC_CHECK_FUNCS(getline,, AC_MSG_ERROR("getline\(\) is needed by $PACKAGE"),
	       $CONF_IN_LDFLAGS)

AC_CHECK_FUNCS(fopencookie,, AC_MSG_WARN("disabled gzipped files support"),
	       $CONF_IN_LDFLAGS)

AC_CHECK_FUNCS(rpmGetRpmlibProvides,, 
AC_MSG_WARN("poldek will not work fine with rpmlib\(...\) capabilities"),
$CONF_IN_LDFLAGS)


AC_CHECK_FUNCS(EVP_DigestInit,, 
	AC_CHECK_LIB(crypto, EVP_DigestInit,, 
	AC_MSG_ERROR("libcryto is needed by $PACKAGE")), $CONF_IN_LDFLAGS)

AC_OUTPUT([
		shell/Makefile
	        vfile/Makefile
		Makefile
		poldek.spec
	])
